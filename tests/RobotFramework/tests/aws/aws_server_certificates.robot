*** Settings ***
Documentation       Verify that thin-edge.io can successfully connect to AWS IoT Core

Resource            ../../resources/common.resource
Library             ThinEdgeIO
Library             AWS

Suite Setup         Custom Setup
Test Teardown       Get Logs

Test Tags           theme:mqtt    theme:aws    test:on_demand


*** Test Cases ***
Connection check succeeds
    [Template]    Connect using certificates generated by AWS
    builtin_bridge=false
    builtin_bridge=true

Connection check fails when the connection was rejected
    [Documentation]    Check that if the connection is rejected by AWS Cloud, e.g. due to cert not being trusted, then we return a non-zero exit code.
    [Setup]    Set untrusted certificate
    Execute Command    tedge reconnect aws    exp_exit_code=!0    retries=0
    [Teardown]    Restore previous certificates


*** Keywords ***
Custom Setup
    ${DEVICE_SN}=    Setup
    Set Suite Variable    ${DEVICE_SN}

    ${aws}=    AWS.Create Thing With Self-Signed Certificate    name=${DEVICE_SN}
    Execute Command    printf -- '${aws.private_key}' > $(tedge config get device.key_path)
    Execute Command    printf -- '${aws.public_key}' > $(tedge config get device.cert_path)
    Execute Command    sudo tedge config set aws.url ${aws.url}

Connect using certificates generated by AWS
    [Arguments]    ${builtin_bridge}
    Execute Command    tedge config set mqtt.bridge.built_in ${builtin_bridge}
    ${stdout}=    Execute Command    tedge reconnect aws    retries=0
    Should Not Contain    ${stdout}    Warning: Bridge has been configured, but Aws connection check failed
    ThinEdgeIO.Service Health Status Should Be Up    tedge-mapper-aws

    ThinEdgeIO.Bridge Should Be Up    aws

    # Check telemetry data
    ${cloud_topic}=    AWS.Get Cloud Telemetry Topic
    ...    common_name=${DEVICE_SN}
    ...    te_topic=te/device/main///m/environment
    AWS.Start MQTT Logger
    Execute Command    tedge mqtt pub te/device/main///m/environment '{"outside_temp":28.9}'
    ${messages}=    AWS.Should Have MQTT Messages    topic=${cloud_topic}    min_count=1
    Should Not Be Empty    ${messages}

    # Send data to thin-edge.io
    ${cloud_cmd_topic}=    AWS.Get Cloud Command Topic    ${DEVICE_SN}    topic_suffix=foo/bar
    AWS.Publish MQTT Message    ${cloud_cmd_topic}    {"some":"payload"}

    ${local_cmd_topic}=    AWS.Get Local Command Topic    topic_suffix=foo/bar
    ThinEdgeIO.Should Have MQTT Messages    topic=${local_cmd_topic}    message_contains={"some":"payload"}

Set untrusted certificate
    VAR    ${cert_dir}=    /etc/tedge/device-certs
    Execute Command    mkdir ${cert_dir}/bak
    Execute Command    mv ${cert_dir}/*.pem ${cert_dir}/bak
    Execute Command    tedge cert create aws --device-id ${DEVICE_SN}

Restore previous certificates
    VAR    ${cert_dir}=    /etc/tedge/device-certs
    Execute Command    rm ${cert_dir}/*.pem && mv ${cert_dir}/bak/* ${cert_dir}/
